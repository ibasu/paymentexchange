buildscript {
	dependencies {
		classpath 'org.jsonschema2pojo:jsonschema2pojo-gradle-plugin:1.1.1'
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.0'
	id 'io.spring.dependency-management' version '1.1.2'
	id 'jacoco'
}

apply plugin: 'jsonschema2pojo'

group = 'au.com.wex'
version = '0.0.1-SNAPSHOT'

allprojects {
	gradle.projectsEvaluated {
		tasks.withType(JavaCompile) {
			options.compilerArgs << "-Xlint:unchecked"
		}
	}

}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

sourceSets {
	unitTest {
		java.srcDirs = ['src/test/java/unit']
		compileClasspath += sourceSets.test.runtimeClasspath
		runtimeClasspath += sourceSets.test.runtimeClasspath
		resources.srcDir 'src/test/resources'
	}

	blackboxTest {
		java.srcDirs = ['src/test/java/blackbox']
		compileClasspath += sourceSets.test.runtimeClasspath
		runtimeClasspath += sourceSets.test.runtimeClasspath
		resources.srcDir 'src/test/resources'
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.1.0'

	compileOnly 'org.projectlombok:lombok'
	compileOnly("javax.servlet:javax.servlet-api:4.0.1")
	implementation 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'

	implementation 'org.hibernate:hibernate-validator:8.0.0.Final'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation "org.wiremock:wiremock-standalone:3.0.3"
	testCompileOnly 'org.apiguardian:apiguardian-api:1.1.2'
}

task unitTest(type: Test) {
	description = 'Runs the unit tests.'
	testClassesDirs = sourceSets.unitTest.output.classesDirs
	classpath = sourceSets.unitTest.runtimeClasspath
}

task blackboxTest(type: Test) {
	description = 'Runs the blackbox tests.'
	testClassesDirs = sourceSets.blackboxTest.output.classesDirs
	classpath = sourceSets.blackboxTest.runtimeClasspath
}

test {
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
}

jacoco {
	toolVersion = "0.8.9"
	reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

test.doFirst {
	systemProperty 'spring.profiles.active', 'test'
}

tasks.named('test') {
	systemProperty 'spring.profiles.active', 'test'
	useJUnitPlatform()
}

jsonSchema2Pojo {
	targetDirectory = file("${project.buildDir}/generated-sources/treasury")
	targetPackage = "au.com.wex.generated.fiscaldata.pojo"
	generateBuilders = true
	includeAdditionalProperties = false
	source = files("${sourceSets.main.output.resourcesDir}/json/")
	annotationStyle = 'jackson'
	sourceType = 'jsonschema'

}

springBoot {
	buildInfo()
}

defaultTasks = ['clean', 'build']